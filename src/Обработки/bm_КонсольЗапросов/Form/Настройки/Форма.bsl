
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, КонсольЗапросов_СтрокаНастроек());
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФорматЧислаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Конструктор = Новый КонструкторФорматнойСтроки(ЭтотОбъект.НастройкаФорматЧисла);
	Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторФорматнойСтроки_Завершение", ЭтотОбъект, Новый Структура());
	Конструктор.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторФорматнойСтроки_Завершение(Текст, ДополнительныеПараметры) Экспорт
	
	Если Текст <> Неопределено Тогда
		ЭтотОбъект.НастройкаФорматЧисла = Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ЗначениеВыбора = Новый Структура(КонсольЗапросов_СтрокаНастроек());
	ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ЭтотОбъект);
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КонсольЗапросов_СтрокаНастроек()
	
	Возврат "НастройкаМаксКоличествоСтрок,НастройкаВыводитьСвойстваВыборки,НастройкаВыделятьОтрицательные,НастройкаМаксШиринаКолонок,НастройкаПереноситьТекст,НастройкаФорматЧисла";
	
КонецФункции






