#Область СвойстваЭлементовФормы

// Элементы.ПодборТипа.ПоложениеЗаголовка 						= ПоложениеЗаголовкаЭлементаФормы.Верх;
// Элементы.БыстрыйВыбор.ПоложениеЗаголовка 					= ПоложениеЗаголовкаЭлементаФормы.Верх;
// Элементы.ТипЗначения.ПоложениеЗаголовка 						= ПоложениеЗаголовкаЭлементаФормы.Верх;

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ПодборТипа.Видимость 	= Параметры.ПодборТипа;
	Элементы.БыстрыйВыбор.Видимость = Параметры.БыстрыйВыбор;
	
	Если Параметры.ТипЗначения = "Таблица значений" Тогда
		ЭтотОбъект.ПодборТипа = "Таблица значений";		
	ИначеЕсли Параметры.ТипЗначения.СодержитТип(Тип("УникальныйИдентификатор")) Тогда
		ЭтотОбъект.ПодборТипа = "Уникальный идентификатор";
	ИначеЕсли Параметры.ТипЗначения.СодержитТип(Тип("МоментВремени")) Тогда
		ЭтотОбъект.ПодборТипа = "Момент времени";
	ИначеЕсли Параметры.ТипЗначения.СодержитТип(Тип("ВидДвиженияНакопления")) Тогда
		ЭтотОбъект.ПодборТипа = "Вид движения накопления";
	ИначеЕсли Параметры.ТипЗначения.СодержитТип(Тип("ВидДвиженияБухгалтерии")) Тогда
		ЭтотОбъект.ПодборТипа = "Вид движения бухгалтерии";
	ИначеЕсли Параметры.ТипЗначения.СодержитТип(Тип("ВидСчета")) Тогда
		ЭтотОбъект.ПодборТипа = "Вид счета";		
	Иначе
		ЭтотОбъект.ПодборТипа  = "Стандартный выбор";
	КонецЕсли;
	
	Элементы.ПодборТипа.РежимВыбораИзСписка 	= Истина;
	Элементы.ПодборТипа.СписокВыбора.ЗагрузитьЗначения(СписокВыбораПодбораТипа());	
	ЗаполнитьСоответствиеТиповПодбораТипа();
	
	Элементы.БыстрыйВыбор.РежимВыбораИзСписка 	= Истина;
	Элементы.БыстрыйВыбор.СписокВыбора.ЗагрузитьЗначения(СписокВыбораБыстрогоВыбора());	
	ЗаполнитьСоответствиеТиповБыстрогоВыбора();
	
	Если ТипЗнч(Параметры.ТипЗначения) = Тип("ОписаниеТипов") Тогда
		ЭтотОбъект.ТипЗначения = Параметры.ТипЗначения;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ТипЗначения) = Тип("ОписаниеТипов")
		И Параметры.ТипЗначения.СодержитТип(Тип("Null")) Тогда
		ЭтотОбъект.ИспользоватьNULL = Истина;
	КонецЕсли;	
	
	Если ЭтотОбъект.ПодборТипа  = "Стандартный выбор" Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.ТипЗначения;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТипаПриИзменении(Элемент)
	
	ОписаниеТипов = ЭтотОбъект.СоответствиеТиповПодбораТипа.Получить(ЭтотОбъект.ПодборТипа);
	ЭтотОбъект.ТипЗначения 	= ОписаниеТипов;
	ЭтотОбъект.БыстрыйВыбор = "";
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйВыборПриИзменении(Элемент)
	
	ЭтотОбъект.ТипЗначения = ЭтотОбъект.СоответствиеТиповБыстрогоВыбора.Получить(ЭтотОбъект.БыстрыйВыбор);
	ЭтотОбъект.ПодборТипа = "Стандартный выбор";
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	
	ЭтотОбъект.ПодборТипа 	= "Стандартный выбор";
	ЭтотОбъект.БыстрыйВыбор = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЭтотОбъект.ПодборТипа = "Таблица значений" Тогда
		Значение = ЭтотОбъект.ПодборТипа;
	Иначе
		Значение = ЭтотОбъект.ТипЗначения;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("ОписаниеТипов") И ЭтотОбъект.ИспользоватьNULL И Не Значение.СодержитТип(Тип("Null")) Тогда
		Значение = Новый ОписаниеТипов(Значение, "Null");
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("ОписаниеТипов") И Не ЭтотОбъект.ИспользоватьNULL И Значение.СодержитТип(Тип("Null")) Тогда
		Значение = Новый ОписаниеТипов(Значение, , "Null");
	КонецЕсли;	
	
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеТиповПодбораТипа()
	
	Соответствие = Новый Соответствие();
	Соответствие.Вставить("Стандартный выбор", 			Новый ОписаниеТипов());
	Соответствие.Вставить("Таблица значений", 			Новый ОписаниеТипов());
	Соответствие.Вставить("Уникальный идентификатор", 	Новый ОписаниеТипов("УникальныйИдентификатор"));
	Соответствие.Вставить("Момент времени", 			Новый ОписаниеТипов("МоментВремени"));
	Соответствие.Вставить("Вид движения накопления", 	Новый ОписаниеТипов("ВидДвиженияНакопления"));
	Соответствие.Вставить("Вид движения бухгалтерии", 	Новый ОписаниеТипов("ВидДвиженияБухгалтерии"));
	Соответствие.Вставить("Вид счета", 					Новый ОписаниеТипов("ВидСчета"));
	
	ЭтотОбъект.СоответствиеТиповПодбораТипа = Новый ФиксированноеСоответствие(Соответствие);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокВыбораПодбораТипа()
	
	МассивЗначений = Новый Массив();
	МассивЗначений.Добавить("Стандартный выбор");
	МассивЗначений.Добавить("Таблица значений");
	МассивЗначений.Добавить("Уникальный идентификатор");
	МассивЗначений.Добавить("Момент времени");
	МассивЗначений.Добавить("Вид движения накопления");
	МассивЗначений.Добавить("Вид движения бухгалтерии");
	МассивЗначений.Добавить("Вид счета");
	
	Возврат МассивЗначений;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСоответствиеТиповБыстрогоВыбора()
	
	Соответствие = Новый Соответствие();
	Соответствие.Вставить("Число(15, 2)", 		Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2)));
	Соответствие.Вставить("Число(15, 3)", 		Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 3)));
	Соответствие.Вставить("Строка(100, П)", 	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная)));
	Соответствие.Вставить("Дата", 				Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Соответствие.Вставить("Булево", 			Новый ОписаниеТипов("Булево"));
	
	ЭтотОбъект.СоответствиеТиповБыстрогоВыбора = Новый ФиксированноеСоответствие(Соответствие);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокВыбораБыстрогоВыбора()
	
	МассивЗначений = Новый Массив();
	МассивЗначений.Добавить("Число(15, 2)");
	МассивЗначений.Добавить("Число(15, 3)");
	МассивЗначений.Добавить("Строка(100, П)");
	МассивЗначений.Добавить("Дата");
	МассивЗначений.Добавить("Булево");
	
	Возврат МассивЗначений;
	
КонецФункции












