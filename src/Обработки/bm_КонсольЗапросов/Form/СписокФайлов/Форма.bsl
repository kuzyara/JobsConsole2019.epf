#Область СвойстваЭлементовФормы

// Элементы.ФормаОчистить.ТолькоВоВсехДействиях = Истина

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВоВременномХранилище = ПолучитьИзВременногоХранилища(Параметры.АдресСписокФайлов);
	ЭтотОбъект.СписокФайлов.Загрузить(АдресВоВременномХранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И ЭтотОбъект.ФлагИзмененияСписка Тогда
		АдресВоВременномХранилище = Форма_ТаблицаЗначенийВоВременноеХранилище("СписокФайлов");
		Оповестить("ИзменениеСпискаФайлов", Новый Структура("АдресСписокФайлов", АдресВоВременномХранилище));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.СписокФайлов.ТекущиеДанные;
	ПрочитатьФайл(ТекДанные.ПутьКФайлу);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПослеУдаления(Элемент)
	
	ЭтотОбъект.ФлагИзмененияСписка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Прочитать(Команда)
	
	ТекДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьФайл(ТекДанные.ПутьКФайлу);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Закрыть(Новый Структура("Очистить"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайл(ПутьКФайлу)
	
	Файл = Новый Файл(ПутьКФайлу);
	Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("Прочитать_Завершение", ЭтотОбъект, Новый Структура("ПутьКФайлу", ПутьКФайлу)));
	
КонецПроцедуры

&НаКлиенте
Процедура Прочитать_Завершение(Существует, ДополнительныеПараметры) Экспорт
	
	Если Существует Тогда
		Закрыть(Новый Структура("ПутьКФайлу", ДополнительныеПараметры.ПутьКФайлу));
	Иначе
		ПоказатьПредупреждение(, "Файл не найден");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Форма_ТаблицаЗначенийВоВременноеХранилище(Знач ИмяРеквизита)
	
	Возврат ПоместитьВоВременноеХранилище(ЭтотОбъект[ИмяРеквизита].Выгрузить());
	
КонецФункции












