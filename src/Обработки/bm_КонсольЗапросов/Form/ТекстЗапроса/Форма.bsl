
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОтображаемыйТекстЗапроса") Тогда 
		ЭтотОбъект.ОтображаемыйТекстЗапроса.УстановитьТекст(Параметры.ОтображаемыйТекстЗапроса);
	КонецЕсли;
	
	Если Параметры.Источник = "СхемаЗапроса" Тогда
		
		ЭтотОбъект.СхемаЗапросаВХ = Параметры.СхемаЗапросаВХ;
		ЭтотОбъект.ИндексЗапроса  = Параметры.ИндексЗапроса;
		
		СхемаЗапроса = ПолучитьИзВременногоХранилища(ЭтотОбъект.СхемаЗапросаВХ);
		Текст = СхемаЗапроса_ТекстЗапроса(СхемаЗапроса, ЭтотОбъект.ИндексЗапроса);	
		ЭтотОбъект.ОтображаемыйТекстЗапроса.УстановитьТекст(Текст);
		
	Иначе
		Элементы.ФормаОК.Видимость = Ложь;
		Элементы.ОтображаемыйТекстЗапроса.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ОК(Команда)
	
	Попытка
		СхемаЗапроса_УстановитьТекстЗапроса(ЭтотОбъект.СхемаЗапросаВХ, ЭтотОбъект.ИндексЗапроса, ЭтотОбъект.ОтображаемыйТекстЗапроса.ПолучитьТекст());
		ЭтотОбъект.Закрыть(Истина);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
    	ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

//////////////////////////////////////
// Библиотека

&НаСервереБезКонтекста
Функция СхемаЗапроса_ТекстЗапроса(Знач СхемаЗапроса, Знач ИндексЗапроса)
	
	Элемент = СхемаЗапроса.ПакетЗапросов[ИндексЗапроса];
	
	Если ТипЗнч(Элемент) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
		Текст = СхемаЗапроса.ПакетЗапросов[ИндексЗапроса].ПолучитьТекстЗапроса(); 
	ИначеЕсли ТипЗнч(Элемент) = Тип("ЗапросУничтоженияТаблицыСхемыЗапроса") Тогда
		Текст = "УНИЧТОЖИТЬ " + Элемент.ИмяТаблицы;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаСервереБезКонтекста
Функция СхемаЗапроса_УстановитьТекстЗапроса(Знач АдресВХ, Знач ИндексЗапроса, Знач Текст)
	
	СхемаЗапроса = ПолучитьИзВременногоХранилища(АдресВХ);
	СхемаЗапроса.ПакетЗапросов[ИндексЗапроса].УстановитьТекстЗапроса(Текст);
	
КонецФункции