Перем тзСтруктураБД Экспорт;
Перем РегВыр Экспорт;

Перем тзКешИспользуемыхТаблиц;
Перем тзКешИспользуемыхПолей;
Перем _КонецОператора;

Перем флНеОчищатьСтрокиРЛС;

Процедура ПреобразоватьНажатие(Элемент)
    Запрос = ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	
	//уберем строки, порожденные RLS
	Если Не флНеОчищатьСтрокиРЛС Тогда
		ИсключитьRLS(Запрос);
	КонецЕсли;	
	
    ЗаменитьСтандартныеКоманды(Запрос);
	
	Выражение = СокрЛП(Запрос);	
	
	РегВыр.Pattern = "([_A-Za-zА-Я0-9\]\[])+"; //([_A-Za-z0-9\.\""\[\]])*
	Найденные = ВыполнитьПоискСКонтролемОшибок(РегВыр, Выражение);
	
	//переименуем таблицы и сделаем кеш нужных строк из тзСтруктураБД
	Для н = 0 По Найденные.Count()-1 Цикл
		Значение = Найденные.Item(н).Value;
		Если Найти(ВРЕГ(Значение), "_FLD") > 0 Тогда
			//пропускаем
		Иначе	
			ВыполнитьПреобразованиеДляИмениТаблицы(Запрос, Значение);
		КонецЕсли;
	КонецЦикла;	
	
	//обработаем реквизиты
	РегВыр.Pattern = "(_)?[Ff][Ll][Dd]([0-9_A-Za-z])+";
	Найденные = ВыполнитьПоискСКонтролемОшибок(РегВыр, Запрос);
	
	тзКешИспользуемыхТаблиц.Свернуть("Поля, ИмяТаблицыХранения, Метаданные");
	тзКешИспользуемыхПолей.Свернуть("ИмяПоляХранения, ИмяПоля");
		
	Для н = 0 По Найденные.Count()-1 Цикл
		ИмяРеквизита = Найденные.Item(н).Value;
		
		//по кешу
		СтрокаСПараметрами = НайтиНаименованиеРеквизита1С(ИмяРеквизита);	
		
		Если СтрокаСПараметрами <> Неопределено Тогда
			Запрос = СтрЗаменить(Запрос, ИмяРеквизита, ?(ПустаяСтрока(СтрокаСПараметрами.ИмяПоля),ИмяРеквизита,СтрокаСПараметрами.ИмяПоля));	
			//кешируем
			ЗаполнитьЗначенияСвойств(тзКешИспользуемыхПолей.Добавить(), СтрокаСПараметрами);
		КонецЕсли;	
	КонецЦикла;	
	
	Запрос = СтрЗаменить(Запрос,".ТабличнаяЧасть.",".");
	
	ЗаменитьВопросыНаПараметры(Запрос);
	ЗаменитьИдентификаторыНаПараметры(Запрос);
	
	ЭлементыФормы.ТекстРезультат.УстановитьТекст(Запрос);
	
	//TODO:
	//1. вызов хранимой процедуры в запрос (автоматичестки, если ЛЕВ = exec sp_executesql N)
	//2. можно еще преобразовать даты и касты в вид 1с	
	//3. убирать вложенные запросы по RLS
	
	ЗаполнитьТаблицуПараметров();

КонецПроцедуры

Процедура ЗаполнитьТаблицуПараметров()

	//N'@P1 nvarchar(4000),@P2 nvarchar(4000),@P3 numeric(10),@P4 numeric(10),@P5 nvarchar(4000),@P6 nvarchar(4000),@P7 numeric(10),@P8 numeric(10),@P9 numeric(10),@P10 nvarchar(4000),@P11 nvarchar(4000),@P12 nvarchar(4000),@P13 nvarchar(4000),@P14 nvarchar(4000),@P15 nvarchar(4000),@P16 nvarchar(4000),@P17 datetime2(3),@P18 varbinary(16),@P19 numeric(10),@P20 numeric(10),@P21 numeric(10),@P22 numeric(10),@P23 datetime2(3),@P24 varbinary(16),@P25 numeric(10),@P26 numeric(10),@P27 numeric(10),@P28 numeric(10),@P29 datetime2(3),@P30 datetime2(3),@P31 varbinary(16),@P32 nvarchar(4000),@P33 varbinary(16),@P34 nvarchar(4000),@P35 varbinary(16),@P36 numeric(10),@P37 numeric(10),@P38 nvarchar(4000),@P39 varbinary(16),@P40 nvarchar(4000),@P41 varbinary(16),@P42 numeric(10),@P43 nvarchar(4000),@P44 nvarchar(4000),@P45 nvarchar(4000),@P46 nvarchar(4000),@P47 numeric(10),@P48 nvarchar(4000),@P49 nvarchar(4000),@P50 nvarchar(4000),@P51 nvarchar(4000),@P52 nvarchar(4000),@P53 nvarchar(4000),@P54 nvarchar(4000),@P55 nvarchar(4000),@P56 numeric(10),@P57 numeric(10),@P58 nvarchar(4000),@P59 numeric(10),@P60 nvarchar(4000),@P61 numeric(10),@P62 nvarchar(4000),@P63 numeric(10),@P64 nvarchar(4000),@P65 numeric(10),@P66 nvarchar(4000),@P67 numeric(10),@P68 nvarchar(4000),@P69 numeric(10),@P70 nvarchar(4000),@P71 datetime2(3)',N'',N'',5,6,N'',N'',8,1,10,N'',N'',N'',N'',N'',N'',N'','4022-04-10 00:00:00',0xB72E005056997D3311E58D0024DB293E,0,0,0,0,'4022-05-01 00:00:00',0xB72E005056997D3311E58D0024DB293E,0,0,0,0,'4022-04-10 00:00:00','4022-05-01 00:00:00',0xB72E005056997D3311E58D0024DB293E,N'ВПЗ',0x00000000000000000000000000000000,N'ВПЗ',0x00000000000000000000000000000000,1,7,N'Получено водителем',0x00000000000000000000000000000000,N'Получено водителем',0x00000000000000000000000000000000,1,N'',N'',N'',N'',9,N'',N'',N'',N'',N'',N'',N'',N'',11,0,N'',0,N'',0,N'',0,N'',0,N'',0,N'',0,N'','4022-04-10 00:00:00'

	ТаблицаПараметров.Очистить();
	
	СтрокаЗапроса = ЭлементыФормы.ТекстРезультат.ПолучитьТекст();
	СтрокаЗапроса = СтрЗаменить(СтрокаЗапроса, Символы.ПС, "");

	ПозицияПервогоПараметра = Найти(СтрокаЗапроса, "N'");
	Если ПозицияПервогоПараметра = 0 Тогда
		Возврат;
	КонецЕсли;

	СтрокаПараметров = Сред(СтрокаЗапроса, ПозицияПервогоПараметра+2);

	МассивСтрок = СтрРазделить(СтрокаПараметров, ",", Ложь);

	ПозицияПоследнегоПараметра = 0;
	Для Каждого ТекСтрока Из МассивСтрок Цикл
		ТекСтрока = СокрЛП(ТекСтрока);
		Если Найти(ТекСтрока, "@P") > 0 Тогда
			НоваяСтрока = ТаблицаПараметров.Добавить();
			НоваяСтрока.Параметр = ТекСтрока;
			ПозицияПоследнегоПараметра = Найти(СтрокаПараметров, ТекСтрока) + СтрДлина(ТекСтрока);
		КонецЕсли;
	КонецЦикла;

	СтрокаЗначений = Сред(СтрокаПараметров, ПозицияПоследнегоПараметра);
	МассивСтрокЗначений = СтрРазделить(СтрокаЗначений, ",", Ложь);
	Индекс = 0;
	ЧислоПараметров = МассивСтрокЗначений.Количество();
	
	НайденныеИдентификаторы = ВосстановитьЗначение("НайденныеИдентификаторы");
	Для Каждого ТекСтрокаЗначений Из МассивСтрокЗначений Цикл
		Если Индекс < ТаблицаПараметров.Количество() Тогда
			ТаблицаПараметров[Индекс].Значение = ТекСтрокаЗначений;
			//Если СтрДлина(ТекСтрокаЗначений) = СтрДлина("0x839B0D52FD1363834A6670C07CD7ED39") И Лев(ТекСтрокаЗначений, 2) = "0x" Тогда
			//	#Если Клиент Тогда
			//		Состояние(СтрШаблон("Расшифровка %3 строка %1/%2", Индекс, ЧислоПараметров, ТекСтрокаЗначений));
			//	#КонецЕсли
			//	Если ТекСтрокаЗначений = "0x00000000000000000000000000000000" Тогда
			//		Продолжить;
			//	КонецЕсли;

			//	УникальныйИдентификаторОбъекта = ПреобразоватьСлужебныйИдентификатор(СтрЗаменить(ТекСтрокаЗначений, "0x", ""));
			//	ПредставлениеСсылки = "";
			//	Если НайденныеИдентификаторы[УникальныйИдентификаторОбъекта] <> Неопределено Тогда
			//		ПредставлениеСсылки = НайденныеИдентификаторы[УникальныйИдентификаторОбъекта];
			//	Иначе
			//		НайденныеОбъекты = НайтиОбъектыПоУникальномуИдентификатору(УникальныйИдентификаторОбъекта);
			//		Для Каждого НайденныйОбъект Из НайденныеОбъекты Цикл
			//			ПредставлениеСсылки = ПредставлениеСсылки + СтрШаблон(" ( ""%1"" - %2 )", НайденныйОбъект, ИмяТаблицыПоСсылке(НайденныйОбъект));
			//		КонецЦикла;
			//		НайденныеИдентификаторы.Вставить(УникальныйИдентификаторОбъекта, ПредставлениеСсылки);
			//	КонецЕсли;
			//	ТаблицаПараметров[Индекс].Значение = ПредставлениеСсылки;
			//КонецЕсли;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	СохранитьЗначение("НайденныеИдентификаторы", НайденныеИдентификаторы);
КонецПроцедуры

Процедура ЗаменитьВопросыНаПараметры(Запрос)
	Результат = Новый ТекстовыйДокумент;
	НомерПараметра = 0;
	Для Сч = 1 По СтрЧислоСтрок(Запрос) Цикл
		ТекСтрока = СокрЛП(СтрПолучитьСтроку(Запрос, Сч));
		~ОбработатьСтрокуЕщёРазик:
		ПозицияПараметра = СтрНайти(ТекСтрока, "?");
		Если ПозицияПараметра > 0 Тогда
			ТекСтрока = Сред(ТекСтрока, 1, ПозицияПараметра - 1) + СтрШаблон("&p_%1", НомерПараметра) + Сред(ТекСтрока,  ПозицияПараметра + 1);
			НомерПараметра = НомерПараметра + 1;
			Перейти ~ОбработатьСтрокуЕщёРазик;
		КонецЕсли;
		Результат.ДобавитьСтроку(ТекСтрока);
	КонецЦикла;
	Запрос = Результат.ПолучитьТекст();
КонецПроцедуры

Процедура ЗаменитьИдентификаторыНаПараметры(Запрос)
	Результат = Новый ТекстовыйДокумент;
	ЧислоСтрокЗапроса = СтрЧислоСтрок(Запрос);
	НайденныеИдентификаторы = ВосстановитьЗначение("НайденныеИдентификаторы");
	Если НайденныеИдентификаторы = Неопределено Тогда
		НайденныеИдентификаторы = Новый Соответствие;
	КонецЕсли;
	Для Сч = 1 По ЧислоСтрокЗапроса Цикл
		ТекСтрока = СокрЛП(СтрПолучитьСтроку(Запрос, Сч));
		Поз = СтрНайти(ТекСтрока, "0x");
		Если Поз = 0 Тогда
			Результат.ДобавитьСтроку(ТекСтрока);
			Продолжить;
		КонецЕсли;
		ТекИдентификатор = Сред(ТекСтрока, Поз);
		Если СтрДлина(ТекИдентификатор) = СтрДлина("0x839B0D52FD1363834A6670C07CD7ED39") И Лев(ТекИдентификатор, 2) = "0x" Тогда
			Если ТекИдентификатор = "0x00000000000000000000000000000000" Тогда
				Результат.ДобавитьСтроку(ТекСтрока);
				Продолжить;
			КонецЕсли;
			#Если Клиент Тогда
				Состояние(СтрШаблон("Расшифровка %3 строка %1/%2", Сч, ЧислоСтрокЗапроса, ТекИдентификатор));
			#КонецЕсли
			
			УникальныйИдентификаторОбъекта = ПреобразоватьСлужебныйИдентификатор(СтрЗаменить(ТекИдентификатор, "0x", ""));
		
			ПредставлениеСсылки = "";
			Если НайденныеИдентификаторы[УникальныйИдентификаторОбъекта] <> Неопределено Тогда
				ПредставлениеСсылки = НайденныеИдентификаторы[УникальныйИдентификаторОбъекта];
			Иначе
				НайденныеОбъекты = НайтиОбъектыПоУникальномуИдентификатору(УникальныйИдентификаторОбъекта);
				Для Каждого НайденныйОбъект Из НайденныеОбъекты Цикл
					ПредставлениеСсылки = ПредставлениеСсылки + СтрШаблон(" ( ""%1"" - %2 )", НайденныйОбъект, ИмяТаблицыПоСсылке(НайденныйОбъект));
				КонецЦикла;
				НайденныеИдентификаторы.Вставить(УникальныйИдентификаторОбъекта, ПредставлениеСсылки);
			КонецЕсли;
			ТекСтрока = ТекСтрока + ПредставлениеСсылки;
		КонецЕсли;
		Результат.ДобавитьСтроку(ТекСтрока);
	КонецЦикла;
	СохранитьЗначение("НайденныеИдентификаторы", НайденныеИдентификаторы);
	Запрос = Результат.ПолучитьТекст();
КонецПроцедуры

Функция ИмяТаблицыПоСсылке(Ссылка) Экспорт
    Возврат Метаданные.НайтиПоТипу(ТипЗнч(Ссылка)).ПолноеИмя();
КонецФункции
Функция НайтиОбъектыПоУникальномуИдентификатору(Знач УникальныйИдентификаторОбъекта)
	
	УникальныйИдентификаторОбъектаСтрока = Строка(УникальныйИдентификаторОбъекта);
	
	НайденныеОбъекты = Новый Массив;
	
	МассивСсылочныхМетаданных = Новый Массив;
	МассивСсылочныхМетаданных.Добавить(Метаданные.ПланыОбмена);
	МассивСсылочныхМетаданных.Добавить(Метаданные.ПланыСчетов);
	МассивСсылочныхМетаданных.Добавить(Метаданные.Документы);
	МассивСсылочныхМетаданных.Добавить(Метаданные.Справочники);
	МассивСсылочныхМетаданных.Добавить(Метаданные.ПланыВидовХарактеристик);
	МассивСсылочныхМетаданных.Добавить(Метаданные.ПланыВидовРасчета);
	МассивСсылочныхМетаданных.Добавить(Метаданные.БизнесПроцессы);
	МассивСсылочныхМетаданных.Добавить(Метаданные.Задачи);	

	Для Каждого СсылочныеМетаданные Из МассивСсылочныхМетаданных Цикл
		
		Для Каждого ОбъектМетаданные Из СсылочныеМетаданные Цикл
			
			ПолноеИмяОбъекта = ОбъектМетаданные.ПолноеИмя();
			МенеджерОбъекта = МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта);
			
			СсылкаНаОбъект = МенеджерОбъекта.ПолучитьСсылку(УникальныйИдентификаторОбъекта);
			
			Если СсылкаСуществует(СсылкаНаОбъект, ПолноеИмяОбъекта) Тогда
				НайденныеОбъекты.Добавить(СсылкаНаОбъект);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ОбъектМетаданныхПеречисление Из Метаданные.Перечисления Цикл
		
		МенеджерПеречисления = Перечисления[ОбъектМетаданныхПеречисление.Имя];
		
		Для Каждого ЗначениеПеречисления Из ОбъектМетаданныхПеречисление.ЗначенияПеречисления Цикл
			
			СсылкаНаПеречисление = МенеджерПеречисления[ЗначениеПеречисления.Имя];	
			ИнформацияОСсылке = РасширеннаяИнформацияОСсылке(СсылкаНаПеречисление);
			
			Если ИнформацияОСсылке.ИдентификаторОбъекта = УникальныйИдентификаторОбъектаСтрока Тогда
				НайденныеОбъекты.Добавить(СсылкаНаПеречисление);	
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НайденныеОбъекты;
	
КонецФункции
Функция СсылкаСуществует(ЛюбаяСсылка, ПолноеИмяТипа)
	
    ТекстЗапроса = "
        |ВЫБРАТЬ
        |    Ссылка
        |ИЗ
        |    Справочник.ИмяСправочника КАК Таблица
        |ГДЕ
        |    Ссылка = &Ссылка
        |";
			
    Запрос = Новый Запрос;
    Запрос.Текст = ТекстЗапроса;
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"Справочник.ИмяСправочника",
		ПолноеИмяТипа);
	
    Запрос.УстановитьПараметр("Ссылка", ЛюбаяСсылка);
    
    УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		Результат = Запрос.Выполнить().Пустой();
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
    Возврат НЕ Результат;
    
КонецФункции
Функция РасширеннаяИнформацияОСсылке(СсылкаНаОбъект)
	
	ИнформацияОСсылке = Новый Структура;
	ИнформацияОСсылке.Вставить("ИдентификаторОбъекта", Неопределено);
	ИнформацияОСсылке.Вставить("ИдентификаторТипа", Неопределено);
	ИнформацияОСсылке.Вставить("ЧисловойИдентификаторТипа", Неопределено);
	
	ВнутреннееОпределениеСсылки = ЗначениеВСтрокуВнутр(СсылкаНаОбъект);
	ЧастиВнутреннегоОпределенияСсылки = РазложитьСтрокуВМассивПодстрок(ВнутреннееОпределениеСсылки, ",");
	Если ЧастиВнутреннегоОпределенияСсылки.Количество() = 3 Тогда
		
		ИнформацияОСсылке.ИдентификаторТипа = ЧастиВнутреннегоОпределенияСсылки.Получить(1);
		ВнутреннийИдентификаторСсылки = СтрЗаменить(ЧастиВнутреннегоОпределенияСсылки.Получить(2), "}", "");
		ИнформацияПоСлужебномуПредставлениюСсылки = ИнформацияПоСлужебномуПредставлениюСсылки("(" + ВнутреннийИдентификаторСсылки + ")");
		
		ИнформацияОСсылке.ИдентификаторОбъекта = ИнформацияПоСлужебномуПредставлениюСсылки.ИдентификаторЗначения;
		ИнформацияОСсылке.ЧисловойИдентификаторТипа = ИнформацияПоСлужебномуПредставлениюСсылки.ЧисловойИдентификаторТипа;
		
	Иначе			
		
		Если НЕ СсылкаНаОбъект = Неопределено Тогда
			ИнформацияОСсылке.ИдентификаторОбъекта = СсылкаНаОбъект.УникальныйИдентификатор();
		Иначе
			ИнформацияОСсылке.ИдентификаторОбъекта = "";
		КонецЕсли;
		
	КонецЕсли;
	
	ИнформацияОСсылке.ИдентификаторОбъекта = Строка(ИнформацияОСсылке.ИдентификаторОбъекта);
	
	Возврат ИнформацияОСсылке;

КонецФункции
Функция РазложитьСтрокуВМассивПодстрок(Знач Значение, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, 
	СокращатьНепечатаемыеСимволы = Ложь)
	
	Если Разделитель = "," 
		И ПропускатьПустыеСтроки = Неопределено 
		И СокращатьНепечатаемыеСимволы Тогда 
		
		Результат = СтрРазделить(Значение, ",", Ложь);
		Для Индекс = 0 По Результат.ВГраница() Цикл
			Результат[Индекс] = СокрЛП(Результат[Индекс])
		КонецЦикла;
		Возврат Результат;
		
	КонецЕсли;
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Значение) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = СтрНайти(Значение, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Значение, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Значение = Сред(Значение, Позиция + СтрДлина(Разделитель));
		Позиция = СтрНайти(Значение, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Значение) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Значение));
		Иначе
			Результат.Добавить(Значение);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
Функция ПреобразоватьКЧислу(Знач ИсходноеЗначение)
	
	Попытка
		Возврат Число(ИсходноеЗначение);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции
Функция ИнформацияПоСлужебномуПредставлениюСсылки(Знач ПредставлениеБитойСсылки)
	
	ИнформацияОСсылке = Новый Структура;
	ИнформацияОСсылке.Вставить("ИдентификаторЗначения", Неопределено);
	ИнформацияОСсылке.Вставить("ЧисловойИдентификаторТипа", Неопределено);
	
	НачалоДанных = Найти(ПредставлениеБитойСсылки, "(");
	КонецДанных = Найти(ПредставлениеБитойСсылки, ")");
	ЗначащиеДанныеСсылки = Сред(ПредставлениеБитойСсылки, НачалоДанных + 1,  КонецДанных - НачалоДанных - 1);
	ЧастиИдентификатораСсылки = РазложитьСтрокуВМассивПодстрок(ЗначащиеДанныеСсылки, ":");
	Если ЧастиИдентификатораСсылки.Количество() = 2 Тогда
		
		ЧисловойИдентификаторТипа = ПреобразоватьКЧислу(ЧастиИдентификатораСсылки.Получить(0));
		ИнформацияОСсылке.ЧисловойИдентификаторТипа = ЧисловойИдентификаторТипа;
		
		ИдентификаторЗначения = ЧастиИдентификатораСсылки.Получить(1);		
		Если ЭтоУникальныйИдентификатор(ИдентификаторЗначения) Тогда
			ИнформацияОСсылке.ИдентификаторЗначения = ИдентификаторЗначения;
		Иначе
			ИдентификаторЗначения = ПреобразоватьСлужебныйИдентификатор(ИдентификаторЗначения);
			Если ЭтоУникальныйИдентификатор(ИдентификаторЗначения) Тогда
				ИнформацияОСсылке.ИдентификаторЗначения	= ИдентификаторЗначения;
			Иначе			
				ИнформацияОСсылке.ИдентификаторЗначения = Неопределено;
			КонецЕсли;
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат ИнформацияОСсылке;
	
КонецФункции

Функция МенеджерОбъектаПоПолномуИмени(ПолноеИмя)
	Перем КлассОМ, ИмяОМ, Менеджер;
	
	ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
	
	Если ЧастиИмени.Количество() >= 2 Тогда
		КлассОМ = ЧастиИмени[0];
		ИмяОМ  = ЧастиИмени[1];
	КонецЕсли;
	
	Если      ВРег(КлассОМ) = "ПЛАНОБМЕНА" Тогда
		Менеджер = ПланыОбмена;
		
	ИначеЕсли ВРег(КлассОМ) = "СПРАВОЧНИК" Тогда
		Менеджер = Справочники;
		
	ИначеЕсли ВРег(КлассОМ) = "ДОКУМЕНТ" Тогда
		Менеджер = Документы;
				
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВХАРАКТЕРИСТИК" Тогда
		Менеджер = ПланыВидовХарактеристик;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНСЧЕТОВ" Тогда
		Менеджер = ПланыСчетов;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВРАСЧЕТА" Тогда
		Менеджер = ПланыВидовРасчета;
		
	ИначеЕсли ВРег(КлассОМ) = "БИЗНЕСПРОЦЕСС" Тогда
		Менеджер = БизнесПроцессы;
		
	ИначеЕсли ВРег(КлассОМ) = "ЗАДАЧА" Тогда
		Менеджер = Задачи;
	КонецЕсли;
			
	Если Менеджер <> Неопределено Тогда
		Попытка
			Возврат Менеджер[ИмяОМ];
		Исключение
			Менеджер = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Менеджер;
	
КонецФункции
Функция ПреобразоватьСлужебныйИдентификатор(Знач ИсходныйИдентификатор)
	
	УникальныйИдентификаторСтрокой = Сред(ИсходныйИдентификатор, 25, 8) 
		+ "-"
		+ Сред(ИсходныйИдентификатор, 21, 4) 
		+ "-"
		+ Сред(ИсходныйИдентификатор, 17, 4) 
		+ "-" 
		+ Сред(ИсходныйИдентификатор, 1, 4) 
		+ "-" 
		+ Сред(ИсходныйИдентификатор, 5, 12);	
		
	Если ЭтоУникальныйИдентификатор(УникальныйИдентификаторСтрокой) Тогда
		Возврат Новый УникальныйИдентификатор(УникальныйИдентификаторСтрокой);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

Функция ЭтоУникальныйИдентификатор(Знач УникальныйИдентификаторСтрокой)
	
	РезультатПроверки = Неопределено;
	
	Попытка
		ПроверочныйИдентификатор = Новый УникальныйИдентификатор(УникальныйИдентификаторСтрокой);
		РезультатПроверки = Истина;
	Исключение
		РезультатПроверки = Ложь;
	КонецПопытки;
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура ПриОткрытии()
	РегВыр = Новый COMОбъект("VBScript.RegExp");
	РегВыр.IgnoreCase = Истина; //Игнорируем регистр
	РегВыр.Global = Истина; //Поиск всех вхождений шаблона
    РегВыр.Multiline = Истина;
	
	тзСтруктураБД = ПолучитьСтруктуруХраненияБазыДанных(,Истина);
	тзКешИспользуемыхТаблиц = Новый ТаблицаЗначений;
	тзКешИспользуемыхТаблиц.Колонки.Добавить("Поля");
	тзКешИспользуемыхТаблиц.Колонки.Добавить("ИмяТаблицыХранения");
	тзКешИспользуемыхТаблиц.Колонки.Добавить("Метаданные");
	
	тзКешИспользуемыхПолей = Новый ТаблицаЗначений;
	тзКешИспользуемыхПолей.Колонки.Добавить("ИмяПоляХранения");
	тзКешИспользуемыхПолей.Колонки.Добавить("ИмяПоля");
	
	_КонецОператора = "((^)|([\s()])+)";
	ДобавитьСтрокуВКешПолей("Folder", "ЭтоГруппа");	
	ДобавитьСтрокуВКешПолей("IDRRef", "Ссылка");
	ДобавитьСтрокуВКешПолей("ParentIDRRef", "Родитель");
	ДобавитьСтрокуВКешПолей("Date_Time", "Дата");
	ДобавитьСтрокуВКешПолей("Number", "Номер");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"CASE"+_КонецОператора, "ВЫБОР");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"WHEN"+_КонецОператора, "КОГДА");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"ELSE"+_КонецОператора, "ИНАЧЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"THEN"+_КонецОператора, "ТОГДА");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"END"+_КонецОператора, "КОНЕЦ");
	ДобавитьСтрокуВКешПолей("Active", "Активность");
	ДобавитьСтрокуВКешПолей("Period", "Период");
	ДобавитьСтрокуВКешПолей("Posted", "Проведен");
	ДобавитьСтрокуВКешПолей("RecorderTRef", "Регистратор");
	ДобавитьСтрокуВКешПолей("RecorderRRef", "Регистратор");
	ДобавитьСтрокуВКешПолей("RecordKind", "ВидДвижения");
	ДобавитьСтрокуВКешПолей("Description", "Наименование");
	ДобавитьСтрокуВКешПолей("Code", "Код");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"0x01"+_КонецОператора, "1");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"0x00"+_КонецОператора, "0");
	//ДобавитьСтрокуВКешПолей(_КонецОператора+"WITH((^)|([\s()])*)[(](([\s()])*)NOLOCK(([\s()])*)[)]", "");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"WITH"+_КонецОператора, "");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"NOLOCK"+_КонецОператора, "");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"WHERE"+_КонецОператора, "ГДЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"ON"+_КонецОператора, "ПО");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"AND"+_КонецОператора, "И");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"OR"+_КонецОператора, "ИЛИ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"LEFT((^)|(\s))+OUTER((^)|(\s))+JOIN"+_КонецОператора, "ЛЕВОЕ СОЕДИНЕНИЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"RIGHT((^)|(\s))+OUTER((^)|(\s))+JOIN"+_КонецОператора, "ПРАВОЕ СОЕДИНЕНИЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"INNER((^)|(\s))+JOIN"+_КонецОператора, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"SELECT"+_КонецОператора, "ВЫБРАТЬ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"FROM"+_КонецОператора, "ИЗ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"EXISTS"+_КонецОператора, "СУЩЕСТВУЕТ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"COUNT"+_КонецОператора, "КОЛИЧЕСТВО");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"AVG"+_КонецОператора, "СРЕДНЕЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"MAX"+_КонецОператора, "МАКСИМУМ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"MIN"+_КонецОператора, "МИНИМУМ");
    ДобавитьСтрокуВКешПолей(_КонецОператора+"SUM"+_КонецОператора, "СУММА");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"AS"+_КонецОператора, "КАК");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"IN"+_КонецОператора, "В");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"DISTINCT"+_КонецОператора, "РАЗЛИЧНЫЕ");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"LINENO_"+_КонецОператора, "НомерСтроки");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"GROUP(\s)+BY"+_КонецОператора, "СГРУППИРОВАТЬ ПО");
	ДобавитьСтрокуВКешПолей(_КонецОператора+"ORDER(\s)+BY"+_КонецОператора, "УПОРЯДОЧИТЬ ПО");
	
	///RRef - УникальныйИдентификатор
	///RTRef - ТипСсылка
	///_TYPE - Тип
КонецПроцедуры

Процедура ВыполнитьПреобразованиеДляИмениТаблицы(Запрос, Значение)
	Если Не флНеИспользоватьКеширование Тогда
		СтрокаСПараметрами = тзКешИспользуемыхТаблиц.Найти(Значение, "ИмяТаблицыХранения");		
		Если СтрокаСПараметрами <> Неопределено Тогда
			Запрос = СтрЗаменить(Запрос, Значение, ?(ПустаяСтрока(СтрокаСПараметрами.Метаданные),Значение,СтрокаСПараметрами.Метаданные));	
		КонецЕсли;
	КонецЕсли;
	СтрокаСПараметрами = тзСтруктураБД.Найти(Значение, "ИмяТаблицыХранения");	
	Если СтрокаСПараметрами <> Неопределено Тогда
		Запрос = СтрЗаменить(Запрос, Значение, ?(ПустаяСтрока(СтрокаСПараметрами.Метаданные),Значение,СтрокаСПараметрами.Метаданные));	
		//кешируем
		ЗаполнитьЗначенияСвойств(тзКешИспользуемыхТаблиц.Добавить(), СтрокаСПараметрами);
	КонецЕсли;
КонецПроцедуры

Функция УбратьСкобкиВначалеИВконце(Строка)
	Пока Лев(Строка,1) = "(" ИЛИ Лев(Строка,1) = ")" Цикл
		Строка = Сред(Строка,2);
	КонецЦикла;
	
	Длина = СтрДлина(Строка);
	Пока Прав(Строка,1) = "(" ИЛИ Прав(Строка,1) = ")" И Длина > 0 Цикл
		Строка = Лев(Строка,Длина-1);
		Длина = Длина - 1;
	КонецЦикла;	
	
	Возврат Строка;
КонецФункции

Процедура ЗаменитьСтандартныеКоманды(Запрос)
	Для Каждого СтрокаСПараметрами Из тзКешИспользуемыхПолей Цикл
		РегВыр.Pattern = СтрокаСПараметрами.ИмяПоля;
		Найденные = ВыполнитьПоискСКонтролемОшибок(РегВыр, Запрос);
		
		//переименуем таблицы и сделаем кеш нужных строк из тзСтруктураБД
		Для н = 0 По Найденные.Count()-1 Цикл
			НовоеЗначение =  СтрокаСПараметрами.ИмяПоляХранения;
			Значение = Найденные.Item(н).Value;
			ТЗначение = СокрЛП(УбратьСкобкиВначалеИВконце(СокрЛП(Значение)));
			ТЗначение = СтрЗаменить(Значение, ТЗначение, НовоеЗначение);
			
			Запрос = СтрЗаменить(Запрос,Значение,ТЗначение);
		КонецЦикла;	
	КонецЦикла;	
	
	//породил какую-то хрень вместо хинтов, нужно почистить
	Запрос = СтрЗаменить(Запрос,"()","");
КонецПроцедуры

//надо бы упростить
Процедура ИсключитьRLS(Запрос)
	спИмена = Новый СписокЗначений;
	
	РегВыр.Pattern = "\bT[0-9]+\b";
	Найденные = ВыполнитьПоискСКонтролемОшибок(РегВыр, Запрос);
	
	//переименуем таблицы и сделаем кеш нужных строк из тзСтруктураБД
	Для н = 0 По Найденные.Count()-1 Цикл
		Значение = Найденные.Item(н).Value;
		Если спИмена.НайтиПоЗначению(Значение) = Неопределено Тогда
			 спИмена.Добавить(Значение);
		КонецЕсли;
	КонецЦикла;
	спИмена.СортироватьПоЗначению();
	
	Для Каждого ИмяТаблицы Из спИмена Цикл
		_Any = "([%$#,\.@&*<>{}\w\s()+-=|?]|(^))+";
	    РегВыр.Pattern = "(\bINNER((^)|(\s))+JOIN\b|\bLEFT((^)|(\s))+OUTER((^)|(\s))+JOIN\b)"
	    //+"([\w\s()]|(^))+SELECT([\w\s()]|(^))+"
	    + _Any + "[(]EXISTS[(]SELECT"
	    + _Any + "SDBL_RLS_SIGNAL" + _Any
	    //+ _КонецОператора + "([.\s])+"
	    +"[)]((^)|(\s))+"+ИмяТаблицы
	    +_КонецОператора + "ON"+_КонецОператора+"([_A-Za-zА-Я0-9\]\[\.])+IDRRef = "+ИмяТаблицы+".IDRRef"
	   + "";
	  	Найденные = ВыполнитьПоискСКонтролемОшибок(РегВыр, Запрос);
		
		//переименуем таблицы и сделаем кеш нужных строк из тзСтруктураБД
		Для н = 0 По Найденные.Count()-1 Цикл
			Значение = Найденные.Item(н).Value;
				
			Запрос = СтрЗаменить(Запрос, Значение, ""); //удаляем
		КонецЦикла;
	КонецЦикла;	
 КонецПроцедуры

Функция ВыполнитьЗаменуСКонтролемОшибок(РегВыр, Выражение, ВыражениеЗамены = "") Экспорт
	Попытка
		Возврат РегВыр.Replace(Выражение, ВыражениеЗамены);
	Исключение
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки()); 
		#КонецЕсли 	
	КонецПопытки;	
КонецФункции

Функция ВыполнитьПоискСКонтролемОшибок(РегВыр, Выражение)
	Попытка
		Возврат РегВыр.Execute(Выражение);
	Исключение
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки()); 
		#КонецЕсли 	
	КонецПопытки;	
КонецФункции

Функция НайтиНаименованиеРеквизита1С(Знач ИмяРеквизита)
	Если ПРАВ(ИмяРеквизита,1) = "_" Тогда
		ИмяРеквизита = ЛЕВ(ИмяРеквизита, СтрДлина(ИмяРеквизита)-1);
	КонецЕсли;
    	
	Если Лев(ИмяРеквизита,1)<>"_" Тогда
		ИмяРеквизита = "_" + ИмяРеквизита;
	КонецЕсли;
	
	Если тзКешИспользуемыхТаблиц.Количество() = 0 ИЛИ флНеИспользоватьКеширование Тогда
		тзКешИспользуемыхТаблиц = тзСтруктураБД.Скопировать(, "Поля, ИмяТаблицыХранения, Метаданные");	
	КонецЕсли;
	
	//Если Не флНеИспользоватьКеширование Тогда
		НайденнаяСтрока = тзКешИспользуемыхПолей.Найти(ИмяРеквизита, "ИмяПоляХранения");		
		Если НайденнаяСтрока <> Неопределено Тогда
			Возврат НайденнаяСтрока;
		КонецЕсли;
	//КонецЕсли;
	
	Для Каждого СтрокаСПараметрами Из тзКешИспользуемыхТаблиц Цикл
		НайденнаяСтрока = СтрокаСПараметрами.Поля.Найти(ИмяРеквизита, "ИмяПоляХранения");		
		
		Если НайденнаяСтрока <> Неопределено Тогда
			Возврат НайденнаяСтрока;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Неопределено;
КонецФункции

Процедура ДобавитьСтрокуВКешПолей(ИмяПоля, ИмяПоляХранения)
	ЗаполнитьЗначенияСвойств(тзКешИспользуемыхПолей.Добавить(), Новый Структура("ИмяПоля, ИмяПоляХранения", ИмяПоля, ИмяПоляХранения));
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	РегВыр = NULL;
КонецПроцедуры

Процедура ЗапросПриИзменении(Элемент)
	тзКешИспользуемыхТаблиц.Очистить();
КонецПроцедуры

флНеОчищатьСтрокиРЛС = Истина;